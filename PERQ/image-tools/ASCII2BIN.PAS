program ascii2bin;

{ Takes the input file, which is a bunch of CR-separated ascii decimal
  numbers (generated by bin2ascii.pas here on the PERQ, or a similar utility
  on the mac side) and writes it out.
  The usecase is "make an image on the mac side, generate an ascii version
  in the PIC format (based on the SIGUTILS.PAS utilities) and copy rsx: it
  over.  The perqEmu copy into and out of doesn't work for binaries
}

imports Memory from Memory; { CreateSegment }
imports FileSystem from FileSystem; { FSLookUp }
imports MultiRead from MultiRead; { MultiRead }
imports CmdParse from CmdParse; { NextId }
imports System from System; { UsrCmdLine }


type
    IntType = 0..255;

var
    asciiInputFilename: String;
    binaryOutputFilename: String;
    
    value: IntType;
    incoming: Text;
    outgoing: packed file of IntType;
    
(* ------------- *)
const programName = 'ascii2bin';

procedure Usage;
begin
    Writeln(programName, ': ascii-input binary-output');
    Writeln('    Converts the ascii input file to binary');
    exit(ASCII2BIN);
end;

procedure TwoFiles(var thing1, thing2: String);
var
    appname: CString;
    isSwitch: Boolean;  { unused }
    ch: Char;
begin
    ch := NextId(appname, isSwitch);  { consume application name }
    if ch = CCR then Usage;  { got app name, but no files }
    
    ch := NextId(thing1, isSwitch);
    if ch = CCR then Usage;  {got file1, but ran out before getting file2 }
    
    ch := NextId(thing2, isSwitch);
    if ch <> CCR then Usage;  {got file1 and file2, but still more stuff }
end; { TwoFiles }

(* ------------- *)
    
begin
    TwoFiles(asciiInputFilename, binaryOutputFilename);
    
    reset(incoming, asciiInputFilename);
    rewrite(outgoing, binaryOutputFilename);
    
    while not eof(incoming) do begin
        Readln(incoming, value);
        outgoing^ := value;
        Put(outgoing);
    end;

    close(outgoing);
end.
